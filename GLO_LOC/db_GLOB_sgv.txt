//add Globally beneficial mutations
//latest version -- all loci are one bp
// w = omega = strength selection pressure , 2.5 strong, 4 weak
// sm = sigma normal distribution effect sizes 0.05
// popsize 1000 per deme
// number of loci 100
// migrationrate 0.1

initialize() {
	defineConstant("optimum1", 2);
	defineConstant("optimum2", -2);
	//standard deviation normal distribution
	//defineConstant("sigma", 4.0);
	//defineConstant("sigma_m", 0.05);
	defineConstant("simID", getSeed());
	defineConstant("burnin", 10000);
	defineConstant("generations", 200000);
	//defineConstant("n_loci", 100);
	defineConstant('g_loci', 100);
	//defineConstant("migration", 0.001);
	//defineConstant("mutation_rate", 1e-4);
	
	positions = seqLen(100)*20000+10000;
	defineConstant("POS", positions);

	//selecting positions for globally beneficial mutations 
	glob_positions = sort(sample(10000:2000000, g_loci));
	//make sure they don't overlap (method using setDifference was hugely slow)
	temp = setIntersection(positions, glob_positions);
	while (length(temp)!=0) {
	new = sample(10000:2000000, length(temp));
	for (index in seqLen(length(temp))) glob_positions[which(glob_positions==temp[index])] = new[index]; 
	temp = setIntersection(positions, glob_positions);}

	//adapt so enough neutral sequence at ends
	ends = c(positions[n_loci-1], 2000000);
	rates = c(mutation_rate, 0);
	
	//use spatial dimension to store sum of effect sizes
	initializeSLiMOptions(dimensionality="xy");
	//mutation rate defined for all n_loci sites, add neutral section after last mutation site 
	initializeMutationRate(rates, ends);

	// m1 mutation under local selection
	initializeMutationType("m1", 0.5, "n", 0.0, sigma_m);  //QTL
	//make sure slim includes fixed mutations to calculate fitness
	m1.convertToSubstitution = F;
	//multiple mutations on one locus, stack effect sizes s, only remember last l
	m1.mutationStackPolicy = "s";

	// m2 mutation under global selection
	//initializeMutationType("m2", 0.5, "n", 0.0, sigma_m);  //QTL
	initializeMutationType("m2", 0.5, "f", 0.0);
	//make sure slim includes fixed mutations to calculate fitness
	m2.convertToSubstitution = F;
	//multiple mutations on one locus, stack effect sizes s, only remember last l
	m2.mutationStackPolicy = "s";
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	//continuum of genes, 100 loci
	for (element in positions)
		initializeGenomicElement(g1, element, element);

	// g2 genomic element type: uses m2 for all mutations
	initializeGenomicElementType("g2", m2, 1.0);
	//continuum of genes, 100 loci
	for (element in glob_positions)
		initializeGenomicElement(g2, element, element);
	initializeRecombinationRate(recomb);

	//set working directory
	setwd("/scratch/gbisshop/slim_output/GLO/loc"+folder);
	//setwd("/Users/s1854903/sims/output/test");
	//setwd("/exports/csce/eddie/biology/groups/lohselab/sims/output/LOC/loc"+folder);
}

1 {
	writeFile("DB_glob"+iter+".txt", paste(POS, sep=", "), append=F);
	writeFile("DB_glob"+iter+".txt", "n_loci, selection_sd, mut_sd, migration, mutation_rate, burnin, simID", append=T);
	writeFile("DB_glob"+iter+".txt", paste(c(n_loci, sigma, sigma_m, migration, mutation_rate, burnin, simID), sep=", "), append=T);
	writeFile("DB_glob"+iter+".txt", "time, loc_div, sum_av_div, p1_glob, p2_glob", append=T);
	//size in each deme 1000
	sim.addSubpop("p1", 1000);
	sim.addSubpop("p2", 1000);
	p1.setMigrationRates(p2, migration);
	p2.setMigrationRates(p1, migration);
	gens = seq(1,generations+1, by=500);
	for (gen in gens)
		sim.registerLateEvent(NULL, s1.source, gen, gen);
	sim.deregisterScriptBlock(s1);
	sim.setValue('glob_mean_t2', 2.0);
}

s1 1 late() {
	p1av = float(n_loci);
	p2av = float(n_loci);
	p1muts = p1.individuals.genomes.mutationsOfType(m1);
	p2muts = p2.individuals.genomes.mutationsOfType(m1);
	
	for (pos in seqLen(n_loci)){
		//determine leading allele (based on frequency) and its effect size
		//determine average effect size, weighted by frequency
		//for p1
		//mutations at position pos - convert from 1 to 100
		
		p1pos = p1muts[p1muts.position==POS[pos]];
		p1coeff = p1pos.selectionCoeff;
		p1av[pos] = sum(p1pos.selectionCoeff)/p1.individualCount;
		p2pos = p2muts[p2muts.position==POS[pos]];
		p2coeff = p2pos.selectionCoeff;
		p2av[pos] = sum(p2pos.selectionCoeff)/p2.individualCount;	
				}
					
	loc_av_result = p1av-p2av;
	av_result = sum(loc_av_result);
	
	mean_div = mean(p1.individuals.x) - mean(p2.individuals.x);
	p1_glob = abs(mean(p1.individuals.y) - 2);
	p2_glob = abs(mean(p2.individuals.y) - 2);

	writeFile("DB_glob"+iter+".txt", sim.generation +", "+av_result+", "+mean_div+", "+p1_glob+", "+p2_glob, append=T);
	writeFile("DB_loc"+iter+".txt", sim.generation +", "+paste(loc_av_result, sep=", "), append=T);
	//sim.treeSeqRememberIndividuals(sim.subpopulations.individuals);
	if (all(p1.individuals.countOfMutationsOfType(m1)>0)){
		mut_or = mean(sapply(p1.individuals, 'if (applyValue.countOfMutationsOfType(m1)!=0) length(applyValue.uniqueMutationsOfType(m1)[applyValue.uniqueMutationsOfType(m1).subpopID==1])/length(applyValue.uniqueMutationsOfType(m1));'));
	
		writeFile('mutratio_owndeme'+iter+'.txt', sim.generation+', '+mut_or, append=T);
		//writeFile('mutratio_owndeme'+iter+'.txt', sim.generation+', '+paste(mut_or, sep=', '), append=T);
		}
	}

1: late(){
	inds = sim.subpopulations.individuals;
	inds.x = inds.sumOfMutationsOfType(m1);
	inds.y = inds.sumOfMutationsOfType(m2);
	//updating glob_mean recursively to keep track of mean phenotype in population, so strength of directional
	//selection remains the same throughout the simulation process.
	sim.setValue('glob_mean_t1', sim.getValue('glob_mean_t2'));
	sim.setValue('glob_mean_t2', sim.getValue('glob_mean_t2') + mean(inds.y));
		}

//make QTLs intrinsically neutral
fitness(m1){
	return 1.0;
		}


//calculate fitness based on sum of effect size of all loci
//first 10000 generations to generate SGV
//optimum is 0.0 for both demes

/* make first phase of local selection neutral
1:10000 fitness(NULL, p1){
	return 1 + exp(-((individual.x)^2)/(2*sigma^2));
		}
1:10000 fitness(NULL, p2){
	return 1 + exp(-((individual.x)^2)/(2*sigma^2));
		}
*/

10000 late() {m2.setDistribution("n", 0.0, sigma_m);}

fitness(m2){
	return 1.0;
		}

//calculate fitness based on sum of effect size of all loci
//selection to different optima (2.0 and -2.0)

10001:200001 fitness(NULL, p1){
	d = sqrt((individual.y - sim.getValue('glob_mean_t1'))^2+(individual.x - optimum1)^2);
	return 1 + exp(-(d^2)/(2*sigma^2));
		}
10001:200001 fitness(NULL, p2){
	d = sqrt((individual.y - sim.getValue('glob_mean_t1'))^2+(individual.x - optimum2)^2);
	return 1 + exp(-(d^2)/(2*sigma^2));
		}
	
200001 late() {sim.simulationFinished(); }
